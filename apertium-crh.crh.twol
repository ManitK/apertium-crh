Alphabet

 a â b c ç d e f g ğ h ı i j k l m n ñ o ö p q r s ş t u ü v y z
 A Â B C Ç D E F G Ğ H I   J K L M N Ñ O Ö P Q R S Ş T U Ü V Y Z
 %{i%}:0 %{i%}:i %{i%}:ı %{i%}:u %{i%}:ü
 %{y%}:0 %{y%}:i %{y%}:ı %{y%}:u %{y%}:ü
 %{I%}:i %{I%}:ı %{I%}:u %{I%}:ü
 %{A%}:a
 %{C%}:c %{C%}:ç
 %{D%}:d ! %{D%}:t 
 %{G%}:g %{G%}:q %{G%}:ğ %{G%}:k
 %{E%}:a %{E%}:e %{E%}:y
 %{s%}:0 %{s%}:s
 %{n%}:0 %{n%}:n
 %{l%}:l %{l%}:n
 %{j%}:0 %{j%}:y
 %{U%}:u

 %{ь%}:0
 %{ъ%}:0
 %{☭%}:0

 %>:0 ;

Sets

Vow = a â e ı i o u ö ü 
      A Â E I   O U Ö Ü ; 

Cns = b c ç d f g ğ h j k l m n ñ r p q r s ş t v y z 
      B C Ç D F G Ğ H J K L M N Ñ R P Q R S Ş T V Y Z ; 

NasalCns = m n ñ ; 

VoicelessCns = ç f h k p q s ş t
               Ç F H K P Q S Ş T ;

FrontVow = e i ö ü
           E İ Ö Ü ;

BackVow = a â ı o u
          A Â I O U ;

Rules

"{E} to y after vowel"
%{E%}:y <=> :Vow :0* _ ;

!"Voicing at morpheme boundary"
!Cx:Cy <=>  Vow _ %>: %{s%}:0 :Vow ; 
!               _ %>: %{I%}:  ;
!      except
!           _ %{☭%}: %>: ; 
!      where Cx in ( k p ) ! t 
!            Cy in ( g b ) ! d
!      matched ;

!"Devoicing of non-coronal stem-final voiceless consonants"
!Cx:Cy <=> _ .#. ;
!          _ %>: :Cns/:0* ;
!      except
!           _ %{☭%}: %>: ; 
!      where Cx in ( b g ğ )
!            Cy in ( p k q )
!      matched ;


"Voicing of non-coronal stem-final voiceless consonants"
Cx:Cy <=> _ %>: [ \:Cns ]/:0* ;
      except
          _ %{☭%}: %>: ; 
      where Cx in ( p k q )
            Cy in ( b g ğ )
      matched ;



! k e r e k:g >:0 {I}:i m
! k l a s s i k >:0 {s}:0 {I}:i {n}:0
! k i t a p:b >:0 {s}:0 {I}:ı {n}:0
! y a p >:0 {E}:a 
! t a p >:0 {i}:ı p

"Devoicing of coronal stem-final voiceless consonants"
Cx:Cy <=> :Vow _ .#. ;
          _ %>: [ :Cns - %{D%}: ]/:0* ;
      where Cx in ( d c j )
            Cy in ( t ç ş )
      matched ;

! q a y d

"Deletion of dative {G} after px1sg, px2sg"
%{G%}:0 <=>  [ %{I%}: | %{i%}: ] :NasalCns %>: _ %{A%}: .#. ;
             %{G%}: %>:0 _ ;

! ö z b a ş >:0 {i}:ı {n}:n >:0 {G}:0 {A}:a

! y a p >:0 m {A}:a {G}:ğ >:0 {G}:0 {A}:a

"Epenthesis in some stems"
%{y%}:0 <=> _ [ :Cns :Vow ]/[ :0 | %>: ] ;

"Voicing assimilation of {D}"
%{D%}:t <=> [ :VoicelessCns ]/[ :0 ]* _ ;


!!!!! VOWEL HARMONY !!!!!

"Vowel harmony for {A}"
%{A%}:e <=> [ :FrontVow :Cns :Cns* ]/[ :0 - %{ъ%}: ]* _ ;
            [ :BackVow :Cns :Cns* %{ь%}:0 :Cns* ]/[ :0 ]* _ ;

"Vowel harmony for {U}"
%{U%}:ü <=> [ :FrontVow :Cns :Cns* ]/:0* _ ;

"No {U} after vowels"
%{U%}:0 <=> :Vow/:0* _ ;
